<html>
  <head>
    <title>Grid Path Model</title>
    <script src="../lib/agentscript.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">

    u = ABM.Util # ABM.Util alias
    class MyModel extends ABM.Model
      setup: ->
        # This model is unusual in that all its drawing is done
        # by the single turtle stamping its image and label
        # and drawing a path via its pen being down.
        @refreshPatches = false # for static patches
        @refreshTurtles = false # for static turtles, use drawing layer
        @refreshLinks   = false # not need, no links .. emphasize drawing-only
        # Create a no-op shape, only used for label:
        ABM.Shapes.add "empty", false, (c) ->
        # Set font, use default textAlign, textBaseline (centered)
        u.ctxTextParams @contexts.drawing, "12px sans-serif"

        # globals
        @prob = 1

        # defaults
        @turtles.setDefault "labelColor", [255,255,255]
        @turtles.setDefault "shape", "circle"
        @turtles.setDefault "color", ([210,0,0])
        @turtles.setDefault "size", .5
        @turtles.setDefault "hidden", true # We're using drawing ctx & stamp()
        #@turtles.setDefault "heading", 0 # override promotion to random angle

        @anim.setRate 10

        for p in @patches
          p.color = u.randomGray 160, 190
          p.occupied = false
        @drawGrid [0,0,0]
        [@walker, @printer] = @turtles.create(2)
        @printer.shape = "empty"
        p0 = @patches[0]
        @walker.moveTo p0
        @walker.penDown = true
        @walker.penSize = 2.5
        @printer.moveTo p0
        @walker.p.occupied = true
      step: ->
        @floodFill()
        p = @okNeighbors() # @asSet (n for n in @walker.p.n4 when n.ok)
        n = p.length
        @prob /= n
        @walker.stamp()
        @walker.moveTo p.oneOf()
        @walker.p.occupied = true
        @printer.label = n #""+ n
        @printer.stamp()
        @printer.moveTo @walker# unless @done()
        if @done()
          @walker.stamp()
          @printer.label = @okNeighbors().length;
          @printer.stamp()
          console.log "prob: #{@prob}"
          @stop()

      okNeighbors: -> @asSet (n for n in @walker.p.n4 when n.ok)
      done: -> @patches.last().occupied

      drawGrid: (c) ->
        a = @turtles.create(1)[0]
        a.color = c
        a.penSize *= .4
        a.heading = Math.PI/2
        maxX = @patches.maxX; maxY = @patches.maxY
        for x in [0..maxX]
          a.setXY(x,0); a.penDown = true; a.forward maxY; a.penDown = false
        a.heading = 0
        for y in [0..maxY]
          a.setXY(0,y); a.penDown = true; a.forward maxX; a.penDown = false
        a.die()

      floodFill: ->
        p.ok = false for p in @patches
        pset = [@patches.last()]
        while pset.length > 0
          p.ok = true for p in pset
          pnext = @asSet u.flatten (p.n4 for p in pset)
          pnext.sortById().uniq()
          pset = (n for n in pnext when not n.ok and not n.occupied)
        null

    # div, patchSize, minX, maxX, minY, maxY, isTorus, hasNeighbors
    #   Defaults: 13, -16, 16, -16, 16, false, true
    # model = new MyModel "layers", 30, 0,10, 0,10
    # model.setRootVars() # Debug: Put Model vars in global name space
    # model.start() # Run model immediately after startup initialization
    new MyModel({
      div: "layers",
      size: 50,
      minX: 0,
      maxX: 9,
      minY: 0,
      maxY: 9
    })
    .start()
    .debug()

    </script>
  </head>
  <body>
    <div id="layers"></div>
  </body>
</html>
