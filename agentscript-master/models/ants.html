<html>
  <head>
    <title>Ants Model</title>
    <script src="../lib/agentscript.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">

    u = ABM.Util # aliases
    Shapes = ABM.Shapes
    ColorMaps = ABM.ColorMaps
    class MyModel extends ABM.Model
      setup: ->

        # No optimizations: 10fps
        @patches.usePixels() # 24fps
        # @patches.cacheTurtlesHere() # 9-10fps, not needed & modest overhead
        # two: 27fps, now static colors larger percent of faster patches.

        # Deprecated: @turtles.setUseSprites() # 24->46-48fps!
        @turtles.setDefault "useSprites", true

        # @anim.setRate 25, true # normal: let turtles step faster than drawing
        @anim.setRate 60, false # speed test

        # globals
        @nestColor = "yellow" # [255, 255, 0]  # yellow
        @foodColor = "blue" # [0, 0, 255]    # blue

        # New color model: use maps for diffusion colors
        @nestColorMap = ABM.ColorMaps.NetLogo.yellow
        @foodColorMap = ABM.ColorMaps.NetLogo.blue


        # defaults
        @turtles.setDefault "shape", "bug"
        @turtles.setDefault "color", @foodColor # so default sprite not random color
        # bugSize = 3
        @turtles.setDefault "size", 3 # bugSize

        # UI globals:
        @population = 255
        @maxPheromone = 35
        @diffusionRate = .30
        @evaporationRate = .01
        @wiggleAngle = u.degToRad(30) # radians

        if @turtles.getDefault "useSprites"
          bitSize = @patches.toBits @turtles.getDefault("size") #bugSize
          @nestSprite = Shapes.shapeToSprite "bug", @nestColor, bitSize, "red"
          @foodSprite = Shapes.shapeToSprite "bug", @foodColor, bitSize, "blue"

        @setupPatches()
        @setupTurtles()
      step: ->
        @updateTurtles()
        @updatePatches()

      setupPatches: ->
        for p in @patches
          p.isNest = false ; p.isFood = false
          # REMIND: turtlesOwn etc; NL defaults to 0
          p.foodPheromone = 0; p.nestPheromone = 0
        for p in @patchRect(@patches.maxX-6, 0, 3, 3)
          p.isNest=true; p.color=@nestColor
        for p in @patchRect(@patches.minX+6, 0, 3, 3)
          p.isFood=true; p.color=@foodColor
        null
      setupTurtles: -> # a is turtle
        @turtles.create @population, (a) => # fat arrow for @patches etc
          #a.shape = "bug"
          #a.size = 3
          a.setXY @patches.maxX-6, 0
          @resetTurtle(a, false) # sets a.pheromone to max
      resetTurtle: (a, withFood) ->
        a.carryingFood = withFood
        if a.sprite?
          a.sprite = if withFood then @nestSprite else @foodSprite
          # a.setSprite (if withFood then @nestSprite else @foodSprite)
        else
          a.color = if withFood then @nestColor else @foodColor
          # a.colorStr = u.colorStr a.color if @turtles.staticColors
        a.pheromone = @maxPheromone

      updateTurtles: ->
        for a in @turtles when a.id < @anim.ticks # gradually leave nest
          @wiggleUphill a
          @dropPheromone a
        null # needed to avoid returning an absurd comprehension
      wiggleUphill: (a) ->
        if a.p.isOnEdge()
          # a.rotate Math.PI
          a.rotate u.degToRad(180)
        else
          # nAhead = a.inCone a.p.n, Math.PI, 2
          nAhead = a.inCone a.p.n, u.degToRad(180), 2
          # nAhead = a.p.n.inCone a.p, a.heading, u.degToRad(180), 2
          # nAhead = @patches.inCone a.p, a.heading, u.degToRad(180), 2
          [p, max] = nAhead.maxOneOf ((n) => @targetPheromone(a, n)), true # => for @
          a.face p if max > .001/@maxPheromone
          # p = nAhead.maxOneOf ((n) => @targetPheromone(a, n)) # => for @
          # max = @targetPheromone(a, p)
          # a.face p if max > .001/@maxPheromone
        a.rotate u.randomCentered(@wiggleAngle)
        a.forward 1
      targetPheromone: (a, p) ->
        if a.carryingFood then p.nestPheromone else p.foodPheromone
      dropPheromone: (a) ->
        if (not a.carryingFood and a.p.isFood) or (a.carryingFood and a.p.isNest)
          @resetTurtle a, not a.carryingFood
        if a.carryingFood
        then a.p.foodPheromone += 0.1*a.pheromone
        else a.p.nestPheromone += 0.1*a.pheromone
        a.pheromone *= 0.9

      updatePatches: ->
        @patches.diffuse "nestPheromone", @diffusionRate
        @patches.diffuse "foodPheromone", @diffusionRate
        for p in @patches
          p.foodPheromone *= (1 - @evaporationRate)
          p.nestPheromone *= (1 - @evaporationRate)
          p.foodPheromone = p.nestPheromone = 0 if p.isOnEdge()
          unless (p.isNest or p.isFood) #  or p.isOnEdge()
            if p.foodPheromone > p.nestPheromone
            then p.color = @foodColorMap.scaleColor p.foodPheromone, 0, 1
            else p.color = @nestColorMap.scaleColor p.nestPheromone, 0, 1
            # Old:
            # then p.scaleColor @foodColor, p.foodPheromone
            # else p.scaleColor @nestColor, p.nestPheromone

        null

      # utilities
      patchRect: (x, y, dw, dh) ->
        @patches.patchRect @patches.patch(x,y), dw, dh, true

    # div, patchSize, minX, maxX, minY, maxY, isTorus, hasNeighbors
    #   Defaults: 13, -16, 16, -16, 16, false, true
    window.model = new MyModel {
      div: "layers",
      size: 6,
      minX: -40,
      maxX: 40,
      minY: -40,
      maxY: 40
    }
    model.debug() # Debug: Put Model vars in global name space
    model.start() # Run model immediately after startup initialization
    </script>

  </head>
  <body>
    <div id="layers"></div>
  </body>
</html>
